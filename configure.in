dnl -*- mode: shell-script; -*-
dnl the usual `configure.in' boilerplate
dnl

AC_INIT(config.h.in)
AC_CONFIG_HEADER(config.h)
AC_SUBST(ac_configure_args)

IPSUMDUMP_VERSION=1.14
AC_DEFINE_UNQUOTED(IPSUMDUMP_VERSION, "$IPSUMDUMP_VERSION")
AC_SUBST(IPSUMDUMP_VERSION)

LIBCLICK_VERSION=1.2.2a
AC_SUBST(LIBCLICK_VERSION)
AC_CONFIG_AUX_DIR(libclick-$LIBCLICK_VERSION)

CLICK_INIT('$(top_srcdir)/libclick-'"$LIBCLICK_VERSION")


dnl
dnl locate Click install directory
dnl

AC_ARG_WITH(click, [  --with-click[=DIR]      Click is installed under DIR],
  [clickprefix=$withval; if test -z "$withval" -o "$withval" = yes; then clickprefix=$prefix; fi],
  clickprefix=NONE)

dnl Preset $prefix and $exec_prefix.
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'

if test "$clickprefix" = NONE; then
    LIBCLICK_TARGET=libclick-$LIBCLICK_VERSION
    CLICK_BUILDTOOL='$(top_srcdir)/'"$LIBCLICK_TARGET"'/click-buildtool'
    CLICK_COMPILE='$(top_builddir)/'"$LIBCLICK_TARGET"'/click-compile'
    CLICKINCLUDES='-I$(top_builddir)/'"$LIBCLICK_TARGET"'/include -I$(top_srcdir)/'"$LIBCLICK_TARGET"'/include'
    CLICKLIB='$(top_builddir)/'"$LIBCLICK_TARGET"'/libsrc/libclick.a'
    CLICKLIBFILE=$CLICKLIB
    AC_CONFIG_SUBDIRS(libclick-$LIBCLICK_VERSION)
else
    LIBCLICK_TARGET=
    CLICK_BUILDTOOL="${clickprefix}/bin/click-buildtool"
    CLICK_COMPILE="${clickprefix}/bin/click-compile"
    CLICKINCLUDES=
    CLICKLIB="-L${clickprefix}/lib -lclick"
    CLICKLIBFILE=
fi

AC_SUBST(LIBCLICK_TARGET)
AC_SUBST(CLICK_BUILDTOOL)
AC_SUBST(CLICK_COMPILE)
AC_SUBST(CLICKINCLUDES)
AC_SUBST(CLICKLIB)
AC_SUBST(CLICKLIBFILE)

#if test ! -r $CLICKINCLUDES/click/pathvars.h ; then
#  AC_MSG_ERROR([
#Can't read \`$CLICKINCLUDES/click/pathvars.h'!
#(Are you sure \`$CLICKINCLUDES' is a Click install directory?)])
#fi


dnl
dnl Are Click sources available?
dnl

AC_ARG_WITH(click-build, [  --with-click-build=DIR  DIR is Click build directory],
  [clickbuilddir=$withval], clickbuilddir=NONE)

if test "x$clickbuilddir" != x -a "x$clickbuilddir" != xNONE; then
    if test ! -r $clickbuilddir/config.status; then
	AC_MSG_ERROR([$clickbuilddir is not a Click build directory!])
    fi
    clicksrcdir=`grep '^top_srcdir' $clickbuilddir/Makefile | sed 's/top_srcdir = //'`
    if echo "$clicksrcdir" | grep '^\.' >/dev/null 2>&1; then
	clicksrcdir="$clickbuilddir/$clicksrcdir"
    fi
fi

AC_SUBST(clicksrcdir)
AC_SUBST(clickbuilddir)


dnl
dnl The rest of this file is based on Click `configure.in'. You shouldn't
dnl generally need to change it, although you may want to add to it.
dnl

CLICK_PROG_CC
AC_PROG_CPP
CLICK_PROG_CXX
AC_PROG_CXXCPP

AC_PATH_PROG(AR, ar)
AC_PATH_PROG(LD, ld)
AC_PATH_PROG(RANLIB, ranlib, :)
AC_PATH_PROG(STRIP, strip, :)

CLICK_PROG_INSTALL
CLICK_PROG_AUTOCONF
CLICK_PROG_GMAKE

if test -z "$GMAKE"; then
    AC_MSG_WARN([can't find GNU make, not tracking dependencies])
fi


dnl
dnl Output
dnl

AC_OUTPUT_COMMANDS(for ac_file in $CONFIG_FILES; do
  test -z "$GMAKE" && echo $ac_file | grep Makefile >/dev/null && $ac_given_srcdir/libclick-$LIBCLICK_VERSION/missing gmake $ac_file
done, [GMAKE="$GMAKE"; LIBCLICK_VERSION="$LIBCLICK_VERSION"])
AC_OUTPUT(Makefile src/Makefile)
